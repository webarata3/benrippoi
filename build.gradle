group 'link.webarata3.poi'
version '0.1.0'

apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.poi:poi-ooxml:3.16'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
}

jar {
    baseName = 'benrippoi'
}

/* Source作成用のタスク */
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

// JUnit
// task test
// レポート build/test-results/test/*.xml

// カバレッジ
// JaCoCo plugin
// task jacocoTestReport
apply plugin: 'jacoco'

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = true
    }
}

// FindBugs
// FindBugs plugin
// task check
// レポート build/reports/findbugs/main.xml
apply plugin: 'findbugs'

findbugs {
    ignoreFailures = true
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1'
    }
}

apply plugin: 'com.github.kt3k.coveralls'

